'MILES TAG
'Universal Medic/Ammo Box v0.2
'Revision Date: 17 февраля 2016
'-------------------------------------------------------------
'Добавлена возможность настройки количества здоровья и патронов,
'посылаемых за раз. 
'
'-------------------------------------------------------------
'Written for PICBasic Pro Compiler version 2.50 (www.melabs.com)
'Processor=PIC16F684  (MCLR=OFF; OSC=INTRC)

DEFINE OSC 8
'*************************************************************	
'*************************************************************
'******************** DEFINE CONSTANTS ***********************
'*************************************************************
'*************************************************************
protocol    CON 232           '232=MT Core

mtheader    CON 2400
mtbit0      CON 600
mtbit1      CON 1200
mtspace     CON 600

'*************************************************************
'*************************************************************
'******************** DEFINE VARIABLES ***********************
'*************************************************************
'*************************************************************
bit_counter 	VAR BYTE
databyte 		VAR BYTE	
message         VAR BYTE
message_ID		VAR BYTE

TEMP			VAR BYTE
AMOUNT          var byte    'Временная переменная для количества
RATE            var byte
INC 			var byte
MAX_CAP         var byte

MODE            VAR BYTE

AMOUNT_MED      var byte
AMOUNT_AMMO     var byte
AMOUNT_AIDKIT   var byte

CAP_MED			var byte
CAP_AMMO		var byte

RATE_MED        var byte
RATE_AMMO       var byte
RATE_AIDKIT     var byte
RATE_AIDKIT_INF var byte

STEP_MED		var byte
STEP_AMMO		var byte
STEP_AIDKIT		var byte

'*************************************************************
'*************************************************************
'******************** PIN ASSIGNMENTS ************************
'*************************************************************
'*************************************************************
Symbol CLCK 	    = PORTA.5			'Счётчик +1
Symbol RST       	= PORTA.4			'Сброс дисплеев
Symbol BTN       	= PORTA.2			'Кнопка
Symbol DOT2         = PORTC.1           'Точка на правом индикаторе
SYMBOL DOT1         = PORTC.2           'Точка на левом индикаторе
Symbol PIEZO 		= PORTC.0         	'Зуммер
Symbol IRLED 		= PORTC.5         	'ИК-светодиод

'----------------------------------------------------------------
'-- Резервные порты ---------------------------------------------
'----------------------------------------------------------------

Symbol REZERV1      = PORTA.0           'Зарезервировано
Symbol REZERV2      = PORTA.1           'Зарезервировано
	
'*************************************************************
'*************************************************************
'******************** INITIALIZATION *************************
'*************************************************************
'*************************************************************
'initialize:
	OSCCON = %01111000
	CMCON0 = 7				'turn off comparators
	ANSEL = 0
	
	PR2 = 35				'PWM Period (40K=49, 56K=35) (35 to 55)
	CCPR1L = 15				'PWM Duty Cycle (1 to 15)
	T2CON = 4				'Timer2 = ON; Prescale = 1:1
	CCP1CON = 0				'PWM module off
	OPTION_REG = %000111	'Turn PortA weak pull-ups
	
	TRISA = %000111    		'set PortA directions
	WPUA =  %000111			'enable weak pull-ups PortA
	IOCA =  %000000			'disable PORTA.0 Int On Change

    input PORTA.2

    TRISC = %000000    		'set PortC directions
	
	PORTA = %000000			'обнуление значений PortA
	PORTC = %000000    		'обнуление значений PortC
     
'*************************************************************	
'*************************************************************
'********************* MAIN PROGRAM **************************
'*************************************************************
'*************************************************************
start:

    gosub read_settings
    gosub clear_screen

'----------------------------------------------------------------	
'-- Если кнопка нажата не менее 1 сек, то сигнализируем звуком 
'-- и переходим к процедуре настройки универсальной коробки
'----------------------------------------------------------------
	
    if btn = 0 then
        for temp = 1 to 100
            if btn = 1 then main
            pause 10
        next temp
        high PIEZO          
        PAUSE 1000          
        LOW PIEZO
        goto settings     
    else 
        goto main
    endif        
	
main:    

'----------------------------------------------------------------
'-- Сигнализируем о включении звуковым сигналом и на 1 сек ------
'-- выводим на индикаторы значение текущего режима коробки ------
'----------------------------------------------------------------

    amount = mode
    gosub print_current
    high PIEZO          
	PAUSE 200          
	LOW PIEZO
    pause 800
    gosub clear_screen

'----------------------------------------------------------------
'-- В зависимости от установленного режима коробки устанавливаются
'-- значения количества, продолжительности, а так же посылаемых
'-- коробкой сигналов  ------------------------------------------
'----------------------------------------------------------------
	
    select case mode
        case 1
            message_ID = 0                          'Идентификатор медика
            message = cap_med						'Устанавливаем количество здоровья, посылаемое за раз
			amount = amount_med						'Устаналиваем ёмкость сумки
            rate = rate_med * 100
            goto universal_box
        case 2
            message_ID = 1                          'Идентификатор ящика с патронами
            message = cap_ammo
			amount = amount_ammo
            rate = rate_ammo * 100
            goto universal_box
        case 3
            message_ID = 3                          'Идентификатор пульта
            message = $05                           'Новая игра
            amount = amount_aidkit
            rate = rate_aidkit * 100
            goto universal_box
        case 4
            message_ID = 3                          'идентификатор пульта
            message = $05                           'Новая игра
            rate = rate_aidkit_inf * 100                                                      
            goto aidkit_infinite                          
        case else
            goto shutdown
    end select
               
'----------------------------------------------------------------
'-- Процедура работы в качестве сумки медика, ящика с патронами -
'-- и точки возрождения с количеством срабатываний --------------
'----------------------------------------------------------------

universal_box:

    gosub clear_screen
    gosub print_current

button_loop:

    if btn = 0 then
        if amount > 0 then
            for temp = 1 to rate
                if btn = 1 then button_loop
                pause 10
            next temp
            amount = amount - 1
            gosub send_message
            goto universal_box
        else 
            goto shutdown
        endif
    endif
    nap 3
    goto button_loop

'----------------------------------------------------------------
'-- Процедура работы в качестве бесконечной точки возрождения ---
'----------------------------------------------------------------
	
aidkit_infinite:

    IF btn = 0 THEN                         
        for temp = 1 to rate
            if btn = 1 then aidkit_infinite
            pause 10
        next temp							
        gosub send_message
        goto aidkit_infinite 
    ENDIF
    nap 3
    goto aidkit_infinite

'----------------------------------------------------------------	
'-- Процедура настройки универсальной коробки -------------------
'----------------------------------------------------------------
    
settings:						'Предварительные настройки

    high dot1					'Зажигаем правую точку для индикации режима настройки
    amount = mode
    
setting_mode:

'----------------------------------------------------------------
'-- Выводим на дисплей значение текущего режима -----------------
'----------------------------------------------------------------

    gosub clear_screen
    gosub print_current

set_mode_btn_loop:

'----------------------------------------------------------------
'-- Если кнопка нажата менее 1 сек, то увеличиваем значение -----
'-- текущего режима. Если кнопка нажата 1 сек, то записываем ----
'-- текущее значение режима в память ----------------------------
'----------------------------------------------------------------

    if btn = 0 then
  	    FOR temp = 1 to 100			
            IF btn = 1 THEN 
                if amount = 4 then 
                    amount = 1
                else 
                    amount = amount + 1
                endif
                goto setting_mode
            endif
            PAUSE 10
        NEXT temp
        mode = amount
        write 0,amount
        high PIEZO          
        PAUSE 200          
        LOW PIEZO
        goto pre_setting_amount
    endif
    nap 3
	goto set_mode_btn_loop

'----------------------------------------------------------------
'-- Процедура настройки количества медпакетов, патронов, --------
'-- возрождений (в зависимости от выбранного режима) ------------
'----------------------------------------------------------------

pre_setting_amount:

    select case mode
        case 1                          'Режим сумки медика
            amount = amount_med
        case 2                          'Режим ящика с патронами
            amount = amount_ammo
        case 3                          'Точка возрождения с количеством
            Amount = amount_aidkit
        case 4                          'Точка возрождения бесконечная
            goto pre_setting_rate                          
    end select         

setting_amount:

'----------------------------------------------------------------
'-- Выводим на дисплей записанное в памяти значение количества --
'-- медпакетов, патронов, возрождений (в зависимости от ---------
'-- выбранного режима) ------------------------------------------
'----------------------------------------------------------------

    gosub clear_screen
    gosub print_current
    pause 200
    
set_amount_btn_loop:

'----------------------------------------------------------------
'-- Если кнопка нажата менее 1 сек, то увеличиваем значение -----
'-- количества на 5. Если кнопка нажата 1 сек, то записываем ----
'-- текущее значение количества в память ------------------------
'----------------------------------------------------------------

    if btn = 0 then
  	    FOR temp = 1 to 100			
            IF btn = 1 THEN 
                if amount >= 99 then 
                    amount = 1
                else 
                    amount = amount + 1
                endif
                goto setting_amount
            endif
            PAUSE 10
        NEXT temp
        write mode,amount
        high PIEZO          
        PAUSE 200          
        LOW PIEZO
        goto pre_setting_rate
    endif
    nap 3
	goto set_amount_btn_loop

'----------------------------------------------------------------	
'-- Процедура настройки длительности нажатия кнопки -------------
'----------------------------------------------------------------

pre_setting_rate:

    select case mode
        case 1                          'Режим сумки медика
            amount = rate_med
        case 2                          'Режим ящика с патронами
            amount = rate_ammo
        case 3                          'Точка возрождения с количеством
            Amount = rate_aidkit
        case 4                          'Точка возрождения бесконечная
            amount = rate_aidkit_inf                          
    end select

setting_rate:

'----------------------------------------------------------------
'-- Выводим на дисплей записанное в памяти значение количества --
'-- медпакетов, патронов, возрождений (в зависимости от ---------
'-- выбранного режима) ------------------------------------------
'----------------------------------------------------------------

    gosub clear_screen
    gosub print_current
    pause 200
    
set_rate_btn_loop:

'----------------------------------------------------------------
'-- Если кнопка нажата менее 1 сек, то увеличиваем значение -----
'-- количества на 5. Если кнопка нажата 1 сек, то записываем ----
'-- текущее значение количества в память ------------------------
'----------------------------------------------------------------

    if btn = 0 then
  	    FOR temp = 1 to 100			'
            IF btn = 1 THEN 
                if amount = 5 then 
                    amount = 0
                else 
                    amount = amount + 1
                endif
                goto setting_rate
            endif
            PAUSE 10
        NEXT temp
        temp = mode + 3
        write temp,amount
        high PIEZO          
        PAUSE 200          
        LOW PIEZO
        goto pre_setting_capacity
    endif
    nap 3
	goto set_rate_btn_loop


'----------------------------------------------------------------	
'-- Процедура настройки количества выдаваемых за раз ------------
'-- здоровья или патронов, в зависимости от режима --------------
'----------------------------------------------------------------

pre_setting_capacity:

    select case mode
        case 1                          'Режим сумки медика
            amount = cap_med
            max_cap = 5
            inc = 1
        case 2                          'Режим ящика с патронами
            amount = cap_ammo
            max_cap = 90
            inc = 30
        case 3                          'Точка возрождения с количеством
            goto end_of_settings
        case 4                          'Точка возрождения бесконечная
            goto end_of_settings                          
    end select

setting_capacity:

'----------------------------------------------------------------
'-- Выводим на дисплей записанное в памяти значение количества --
'-- медпакетов, патронов, возрождений (в зависимости от ---------
'-- выбранного режима) ------------------------------------------
'----------------------------------------------------------------

    gosub clear_screen
    gosub print_current
    pause 200
    
set_cap_btn_loop:

'----------------------------------------------------------------
'-- Если кнопка нажата менее 1 сек, то увеличиваем значение -----
'-- количества на 5. Если кнопка нажата 1 сек, то записываем ----
'-- текущее значение количества в память ------------------------
'----------------------------------------------------------------

    if btn = 0 then
  	    FOR temp = 1 to 100			'
            IF btn = 1 THEN 
                if amount >= max_cap then 
                    amount = inc
                else 
                    amount = amount + inc
                endif
                goto setting_capacity
            endif
            PAUSE 10
        NEXT temp
        temp = mode + 7
        write temp,amount
        goto end_of_settings
    endif
    nap 3
	goto set_cap_btn_loop

'----------------------------------------------------------------	
'-- Процедура настройки количества выдаваемых за раз ------------
'-- здоровья или патронов, в зависимости от режима --------------
'----------------------------------------------------------------

'pre_setting_step:

'    select case mode
'        case 1                          'Режим сумки медика
'            amount = step_med
'        case 2                          'Режим ящика с патронами
'            amount = step_ammo
'        case 3                          'Точка возрождения с количеством
'            amount = step_aidkit
'        case 4                          'Точка возрождения бесконечная
'            goto end_of_settings                          
'    end select

'setting_step:

'----------------------------------------------------------------
'-- Выводим на дисплей записанное в памяти значение количества --
'-- медпакетов, патронов, возрождений (в зависимости от ---------
'-- выбранного режима) ------------------------------------------
'----------------------------------------------------------------

'    gosub clear_screen
'    gosub print_current
'    pause 200
    
'set_step_btn_loop:

'----------------------------------------------------------------
'-- Если кнопка нажата менее 1 сек, то увеличиваем значение -----
'-- количества на 5. Если кнопка нажата 1 сек, то записываем ----
'-- текущее значение количества в память ------------------------
'----------------------------------------------------------------

'    if btn = 0 then
'  	    FOR temp = 1 to 100			'
'            IF btn = 1 THEN 
'                if amount = 10 then 
'                    amount = 0
'                else 
'                    amount = amount + 1
'                endif
'                goto setting_step
'            endif
'            PAUSE 10
'        NEXT temp
'        temp = mode + 9
'        write temp,amount
'        goto end_of_settings
'    endif
'    nap 3
'	goto set_step_btn_loop	
	
'----------------------------------------------------------------
'-- Конец  процедуры настройки универсальной коробки ------------
'----------------------------------------------------------------
    
end_of_settings:
    gosub clear_screen
    high dot2
    goto shutdown 

'---------------------------------------------------------------------------

read_settings:              'Чтение настроек из EEPROM

    read 0,mode             'Режим  1 - Сумка медика; 
                            '       2 - Ящик с патронами; 
                            '       3 - Точка воздрождения с количеством; 
                            '       4 - Точка возрождения бесконечная.
    read 1,amount_med       'Ёмкость сумки медика (MODE)
    read 2,amount_ammo      'Ёмкость ящика с патронами (MODE)
    read 3,AMOUNT_AIDKIT    'Количество возрождений (MODE)
    read 4,rate_med         'Длительность нажатия кнопки для сумки медика (MODE+3)
    read 5,rate_ammo        'Длительность нажатия кнопки для ящика с патронами (MODE+3)
    read 6,rate_aidkit      'Длительность нажатия кнопки для точки возрождения с количеством срабатываний (MODE+3)
    read 7,rate_aidkit_inf  'Длительность нажатия кнопки для точки возрождения бесконечной (MODE+3)
	read 8,cap_med			'Количество единиц, выдаваемых сумкой медика за раз (mode+7)
	read 9,cap_ammo			'Количество единиц, выдаваемых ящиком патронов за раз (mode+7)
'	read 10,step_med		'Шаг в настройке ёмкости сумки медика (mode+9)
'	read 11,step_ammo		'Шаг в настройке ёмкости ящика с патронами (mode+9)
'	read 12,step_aidkit		'Шаг в настройке ёмкости точки возрождения с количеством срабатываний (mode+9)	
    return        

'---------------------------------------------------------------------------	
	
clear_screen:   			'Сброс индикаторов

    HIGH RST            
    LOW RST
    return

'---------------------------------------------------------------------------	
    
print_current:  			'Установка на индикаторах текущего значения переменной AMOUNT

    temp = 0
    FOR TEMP = 1 to amount
        HIGH CLCK
        LOW CLCK
    NEXT TEMP
    return    

'---------------------------------------------------------------------------	
	
shutdown:					'Остановка программы и перевод МК в режим энергосбережения

    for temp = 1 to 3
        high piezo
        pause 300
        low piezo
        pause 100
    next temp
    stop    

'*************************************************************	
'*************************************************************
'********************** SUBROUTINES **************************
'*************************************************************
'*************************************************************
send_message:					'send 24 bits 
    high piezo                 
	PAUSE 100          
	LOW piezo
     
	CCP1CON = 12				'send MT header pulse (2.4mS)
    	pauseus mtheader  
	CCP1CON = 0

message_first_byte:
	databyte = message_ID | %10000000     		'(packet_ID=1; sys_msg)  
	GoSub send_byte

message_second_byte:
	databyte = message 			      
	GoSub send_byte

message_third_byte:
	databyte = protocol
	GoSub send_byte
	
	return

'-----------------------------------------------------------
send_byte:						'send 8 bits (databyte)
	For bit_counter = 1 TO 8	'send 8 data bits MSB first
        	pauseus mtspace     'send space -- space between data pulses  
		IF databyte.7 = 1 Then trans1
			CCP1CON = 12		'send zero
            	pauseus mtbit0  
			CCP1CON = 0
			@ rlf	_databyte,F		
		GoTo bypassx
trans1:
			CCP1CON = 12		'send one
            	pauseus mtbit1
			CCP1CON = 0	
			@ rlf	_databyte,F		
bypassx:
	Next bit_counter
	
	Return

'******************************************************

	End


