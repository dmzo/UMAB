'MILES TAG
'Universal Medic/Ammo Box v0.4_alpha_02
'Revision Date: 24 Jul 2016
'-------------------------------------------------------------
'Written for PICBasic Pro Compiler version 2.50 (www.melabs.com)
'Processor=PIC16F684  (MCLR=OFF; OSC=INTRC)

DEFINE OSC 8

'*************************************************************
'******************** DEFINE CONSTANTS ***********************
'*************************************************************
protocol    CON 232           '232=MT Core

mtheader    CON 2400
mtbit0      CON 600
mtbit1      CON 1200
mtspace     CON 600

'*************************************************************
'******************** DEFINE VARIABLES ***********************
'*************************************************************

'-- Переменные для посылки сигнала по ИК -------------

bit_counter 		VAR BYTE	
databyte 		VAR BYTE	
message         	VAR BYTE
message_ID		VAR BYTE

'-- Временные переменные для процедур и циклов вывода информации -------------

TEMP			VAR BYTE	'Временная переменная для всего
TEMP2			var byte	'Временная переменная для вывода значения на индикаторы
AMOUNT          	var byte	'Временная переменная для количества
temp_amount		var byte	'Временная переменная


'-- Переменные для процедуры настройки устройства -------------

RATE            	var byte	'Временная переменная для длительности удержания кнопки
INC 			var byte	'Шаг увеличения
def_value		var byte	'Записанное в ЕЕПРОМ значение
min_value		var byte	'Минимальное значение
MAX_value		var byte	'Максимальное значение
addr			var byte	'Адрес записи значений в ЕЕПРОМ

'-- Переменные для работы устройства -------------

setup			var byte	'0 - обычный режим; 1 - режим настройки
flag			var byte	'Кнопка нажата/отпущена
MODE            	VAR BYTE	'Режим работы коробки
AMOUNT_MED      	var byte	'
AMOUNT_AMMO     	var byte	'
AMOUNT_AIDKIT   	var byte	'
CAP_MED			var byte	'Кол-во выдаваемых за раз жизней
CAP_AMMO		var byte	'Кол-во выдаваемых за раз патронов
RATE_MED        	var byte	'Время удержания кнопки в режиме Медика
RATE_AMMO       	var byte	'Время удержания кнопки в режиме Ящик с патронами
RATE_AIDKIT     	var byte	'Время удержания кнопки в режиме конечной аптечки
RATE_AIDKIT_INF 	var byte	'Время удержания кнопки в режиме бесконечной аптечки

'*************************************************************
'******************** PIN ASSIGNMENTS ************************
'*************************************************************

Symbol CLCK 	    	= PORTA.5		'Счётчик +1
Symbol RST       	= PORTA.4		'Сброс дисплеев
Symbol BTN       	= PORTA.2		'Кнопка
Symbol DOT2         	= PORTC.1           	'Точка на правом индикаторе
SYMBOL DOT1         	= PORTC.2           	'Точка на левом индикаторе
Symbol PIEZO 		= PORTC.0         	'Зуммер
Symbol IRLED 		= PORTC.5         	'ИК-светодиод

'----------------------------------------------------------------
'-- Резервные порты ---------------------------------------------
'----------------------------------------------------------------

Symbol REZERV1      	= PORTA.0           	'Зарезервировано
Symbol REZERV2      	= PORTA.1           	'Зарезервировано

'*************************************************************
'******************** INITIALIZATION *************************
'*************************************************************

'initialize:

OSCCON = %01111000
CMCON0 = 7				'turn off comparators
ANSEL = 0
	
PR2 = 35				'PWM Period (40K=49, 56K=35) (35 to 55)
CCPR1L = 15				'PWM Duty Cycle (1 to 15)
T2CON = 4				'Timer2 = ON; Prescale = 1:1
CCP1CON = 0				'PWM module off
OPTION_REG = %000111			'Turn PortA weak pull-ups
	
TRISA = %000111    			'set PortA directions
WPUA =  %000111				'enable weak pull-ups PortA
IOCA =  %000000				'disable PORTA.0 Int On Change

input PORTA.2

TRISC = %00000				'set PortC directions
	
PORTA = %000000				'обнуление значений PortA
PORTC = %000000    			'обнуление значений PortC

flag = 0
     
'*************************************************************	
'*************************************************************
'********************* MAIN PROGRAM **************************
'*************************************************************
'*************************************************************
start:

gosub read_settings
gosub clear_screen

'----------------------------------------------------------------	
'-- Если кнопка нажата не менее 1 сек, то сигнализируем звуком 
'-- и переходим к процедуре настройки универсальной коробки
'----------------------------------------------------------------
	
if btn = 0 then
	for temp = 1 to 100
		if btn = 1 then main
		pause 10
        next temp
        high PIEZO          
        PAUSE 1000          
        LOW PIEZO
        goto settings     
else 
        goto main
endif        
	
main:    

'----------------------------------------------------------------
'-- Сигнализируем о включении звуковым сигналом и на 1 сек ------
'-- выводим на индикаторы значение текущего режима коробки ------
'----------------------------------------------------------------

setup = 0
amount = mode
gosub print_current
high PIEZO          
PAUSE 200          
LOW PIEZO
pause 800
gosub clear_screen

'----------------------------------------------------------------
'-- В зависимости от установленного режима коробки устанавливаются
'-- значения количества, продолжительности, а так же посылаемых
'-- коробкой сигналов  ------------------------------------------
'----------------------------------------------------------------
	
select case mode
	case 1
		message_ID = 0                          	'Идентификатор медика
		lookup cap_med,[1,5,10,25,50],message		'Устанавливаем количество здоровья, посылаемое за раз
		amount = amount_med				'Устаналиваем ёмкость сумки
		rate = rate_med * 100
		goto universal_box
        case 2
		message_ID = 1                          	'Идентификатор ящика с патронами
		message = cap_ammo
		amount = amount_ammo
		rate = rate_ammo * 100
		goto universal_box
        case 3
		message_ID = 3                          	'Идентификатор пульта
		message = $05                           	'Новая игра
		amount = amount_aidkit
		rate = rate_aidkit * 100
		goto universal_box
        case 4
		message_ID = 3                          	'идентификатор пульта
		message = $05                           	'Новая игра
		rate = rate_aidkit_inf * 100                                                      
		goto aidkit_infinite                          
        case else
		goto shutdown
end select
               
'----------------------------------------------------------------
'-- Процедура работы в качестве сумки медика, ящика с патронами -
'-- и точки возрождения с количеством срабатываний --------------
'----------------------------------------------------------------

universal_box:

gosub clear_screen
gosub print_current

button_loop:

select case rate
	case 0
		if (btn = 0) and (flag = 0) then
			if amount > 0 then
				flag = 1
				amount = amount - 1
				gosub send_message
			else
				goto shutdown
			endif
		endif
		if (btn = 1) and (flag = 1) then
			flag = 0
			goto universal_box
		endif		
		nap 3
		goto button_loop
    	case else
		if btn = 0 then
			if amount > 0 then
				for temp = 1 to rate
					if btn = 1 then button_loop
					pause 10
				next temp
				amount = amount - 1
				gosub send_message
				goto universal_box
			else 
				goto shutdown
			endif
		endif
		nap 3
		goto button_loop    	
end select

'----------------------------------------------------------------
'-- Процедура работы в качестве бесконечной точки возрождения ---
'----------------------------------------------------------------
	
aidkit_infinite:
    
select case rate
	case 0
		if (btn = 0) and (flag = 0) then
			flag = 1
			gosub send_message
		endif
		if (btn = 1) and (flag = 1) then
			flag = 0
			goto aidkit_infinite
		endif
		nap 3
		goto aidkit_infinite
    	case else
		IF btn = 0 THEN                         
			for temp = 1 to rate
				if btn = 1 then aidkit_infinite
				pause 10
			next temp							
			gosub send_message
			goto aidkit_infinite 
		ENDIF
		nap 3
		goto aidkit_infinite    	
end select 

'----------------------------------------------------------------	
'-- Процедура настройки универсальной коробки -------------------
'----------------------------------------------------------------
    
settings:						'Предварительные настройки

high dot1						'Зажигаем правую точку для индикации режима настройки
setup = 1
addr = 0
amount = mode
min_value = 1
max_value = 4
inc = 1
gosub common_settings
mode = amount
    
'----------------------------------------------------------------
'-- Процедура настройки количества медпакетов, патронов, --------
'-- возрождений (в зависимости от выбранного режима) ------------
'----------------------------------------------------------------

pre_setting_amount:

addr = mode
min_value = 1
max_value = 99
select case mode
	case 1                          		'Режим сумки медика
		amount = amount_med
	case 2                          		'Режим ящика с патронами
		amount = amount_ammo
	case 3                          		'Точка возрождения с количеством
		Amount = amount_aidkit
	case 4                          		'Точка возрождения бесконечная
		goto pre_setting_rate                          
end select         
gosub common_settings

'----------------------------------------------------------------	
'-- Процедура настройки длительности нажатия кнопки -------------
'----------------------------------------------------------------

pre_setting_rate:

addr = mode + 3
min_value = 1
max_value = 5
select case mode
	case 1                          		'Режим сумки медика
		amount = rate_med
	case 2                          		'Режим ящика с патронами
		amount = rate_ammo
	case 3                          		'Точка возрождения с количеством
		Amount = rate_aidkit
	case 4                          		'Точка возрождения бесконечная
		amount = rate_aidkit_inf                          
end select
gosub common_settings

'----------------------------------------------------------------	
'-- Процедура настройки количества выдаваемых за раз ------------
'-- здоровья или патронов, в зависимости от режима --------------
'----------------------------------------------------------------

pre_setting_capacity:

addr = mode + 7
select case mode
	case 1                          		'Режим сумки медика
		amount = cap_med
		min_value = 0
		max_value = 4				' 0 = 1; 1 = 5; 2 = 10; 3 = 25; 4 = 50
		inc = 1
	case 2                          		'Режим ящика с патронами
		amount = cap_ammo
		min_value = 30
		max_value = 90
		inc = 30
	case 3,4                          		'Точка возрождения
		goto end_of_settings
end select
gosub common_settings

'----------------------------------------------------------------
'-- Конец  процедуры настройки универсальной коробки ------------
'----------------------------------------------------------------
    
end_of_settings:

gosub clear_screen
high dot2
goto shutdown 

'----------------------------------------------------------------
'-- Общая процедура настройки устройства ------------------------
'----------------------------------------------------------------

common_settings:

'----------------------------------------------------------------
'-- Выводим на дисплей записанное в памяти значение количества --
'-- медпакетов, патронов, возрождений (в зависимости от ---------
'-- выбранного режима) ------------------------------------------
'----------------------------------------------------------------

gosub clear_screen
gosub print_current
pause 200
    
common_settings_loop:

'----------------------------------------------------------------
'-- Если кнопка нажата менее 1 сек, то увеличиваем значение -----
'-- количества на значение INC. Если кнопка нажата 1 сек, то ----
'-- проверяем соответствие установленного значения уже ----------
'-- записанному в ЕЕПРОМ: если отличается, то пишем в ЕЕПРОМ ----
'-- по соответствуюшему адресу ----------------------------------
'----------------------------------------------------------------

if btn = 0 then
	FOR temp = 1 to 100			'
		IF btn = 1 THEN 
			if amount >= max_value then 
				amount = min_value
			else 
				amount = amount + inc
			endif
			
			goto common_settings
		endif
		PAUSE 10
	NEXT temp
	read addr,def_value
	if amount <> def_value then
		write addr,amount
	endif
	high piezo
	pause 200
	low piezo
	return
else
	nap 3
	goto common_settings_loop
endif

'---------------------------------------------------------------------------

read_settings:              		'Чтение настроек из EEPROM

read 0,mode             		'Режим  1 - Сумка медика; 
                            		'       2 - Ящик с патронами; 
                            		'       3 - Точка воздрождения с количеством; 
                            		'       4 - Точка возрождения бесконечная.
read 1,amount_med       		'Ёмкость сумки медика (MODE)
read 2,amount_ammo      		'Ёмкость ящика с патронами (MODE)
read 3,AMOUNT_AIDKIT    		'Количество возрождений (MODE)
read 4,rate_med         		'Длительность нажатия кнопки для сумки медика (MODE+3)
read 5,rate_ammo        		'Длительность нажатия кнопки для ящика с патронами (MODE+3)
read 6,rate_aidkit      		'Длительность нажатия кнопки для точки возрождения с количеством срабатываний (MODE+3)
read 7,rate_aidkit_inf  		'Длительность нажатия кнопки для точки возрождения бесконечной (MODE+3)
read 8,cap_med				'Количество единиц, выдаваемых сумкой медика за раз (mode+7)
read 9,cap_ammo				'Количество единиц, выдаваемых ящиком патронов за раз (mode+7)
return        

'---------------------------------------------------------------------------	
	
clear_screen:   			'Сброс индикаторов

HIGH RST            
LOW RST
return

'---------------------------------------------------------------------------	
    
print_current:  			'Установка на индикаторах текущего значения переменной AMOUNT

temp = 0
if (setup = 1) and (addr = 8) then
	lookup amount,[1,5,10,25,50],temp_amount
	temp2 = temp_amount
else
	temp2 = amount
endif
FOR TEMP = 1 to temp2
	HIGH CLCK
	LOW CLCK
NEXT TEMP
return    

'---------------------------------------------------------------------------	
	
shutdown:				'Остановка программы и перевод МК в режим энергосбережения

for temp = 1 to 3
	high piezo
	pause 300
	low piezo
	pause 100
next temp
stop    

'*************************************************************	
'*************************************************************
'********************** SUBROUTINES **************************
'*************************************************************
'*************************************************************
send_message:					'send 24 bits 

high piezo                 
PAUSE 100          
LOW piezo
     
CCP1CON = 12				'send MT header pulse (2.4mS)
pauseus mtheader  
CCP1CON = 0

message_first_byte:

databyte = message_ID | %10000000     		'(packet_ID=1; sys_msg)  
GoSub send_byte

message_second_byte:

databyte = message 			      
GoSub send_byte

message_third_byte:

databyte = protocol
GoSub send_byte
	
return

'-----------------------------------------------------------
send_byte:						'send 8 bits (databyte)

For bit_counter = 1 TO 8	'send 8 data bits MSB first
	pauseus mtspace     'send space -- space between data pulses  
	IF databyte.7 = 1 Then trans1
	CCP1CON = 12		'send zero
	pauseus mtbit0  
	CCP1CON = 0
	@ rlf	_databyte,F		
	GoTo bypassx

trans1:

	CCP1CON = 12		'send one
	pauseus mtbit1
	CCP1CON = 0	
	@ rlf	_databyte,F		

bypassx:

Next bit_counter
	
Return

'******************************************************

End
